#@TYPE: Machine
#@NAME: TySOM-M-MPFS250T
#@DESCRIPTION: Machine support for TySOM-M-MPFS250T Evaluation Board.
#
# For details on the Evaluation board:
#     https://www.aldec.com/en/products/emulation/tysom_boards
#

require conf/machine/include/riscv/tune-riscv.inc
require include/qspi-flash-nand.inc

MACHINE_FEATURES = "screen keyboard ext2 ext3 serial"
MACHINE_TYPE = "amp"

KERNEL_CLASSES = "kernel-fitimage"
KERNEL_IMAGETYPE = "fitImage"
EXTERNAL_KERNEL_DEVICETREE = "${DEPLOY_DIR_IMAGE}/overlays"

RISCV_SBI_FDT ?=  "mpfs-tysom-m-context-a.dtb"
KERNEL_DTC_FLAGS += "-@"
KERNEL_DEVICETREE ?= "microchip/${RISCV_SBI_FDT}"
KBUILD_DEFCONFIG ?= "mpfs_defconfig"

PREFERRED_PROVIDER_virtual/kernel ?= "mpfs-linux"

PREFERRED_VERSION_openocd-native = "riscv"
PREFERRED_VERSION_openocd = "riscv"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-mpfs"
UBOOT_CONFIG = "aldec_tysom_m_mpfs250t"
UBOOT_CONFIG[aldec_tysom_m_mpfs250t] = "aldec_tysom_m_mpfs250t_amp_defconfig"
UBOOT_ENTRYPOINT = "0x80200000"
UBOOT_DTB_LOADADDRESS = "0x82200000"
UBOOT_DTBO_LOADADDRESS = "0x10 0x02300000"

## Set this to "mmc-tftp" to generate a boot.scr file which should be included
##  in the boot partition. It will try to load a kernel image from mmc and fall back to tftp
UBOOT_ENV = "tftp-mmc"

## Select the YAML file used for building the HSS payload
HSS_PAYLOAD = "amp"

## Set this to "freertos" for a Linux + FreeRTOS demo or "bm" for a Linux + Bare Metal demo
AMP_DEMO = "freertos"
#AMP_DEMO = "bm"

IMAGE_INSTALL:append = " polarfire-soc-amp-examples"

MACHINE_EXTRA_RRECOMMENDS += " kernel-modules"

IMAGE_FSTYPES += "wic wic.gz wic.bmap ext4"
IMAGE_CLASSES:append = " image_type_mtd"

## Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS ?= "--no-fstab-update"

## wic default support
WKS_FILE_DEPENDS ?= " \
    e2fsprogs-native \
    bmap-tools-native \
    u-boot-mpfs \
"
            
IMAGE_BOOT_FILES ?= " \
	fitImage \
        boot.scr.uimg \
                    "
                    
WKS_FILE ?= "mpfs-rootfs.wks"
